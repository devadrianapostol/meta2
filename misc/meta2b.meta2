.SYNTAX PROGRAM

OUT1 = '*1' .OUT('GN1();') 
     / '*2' .OUT('GN2();') 
     / '*' .OUT('CI();')
     / .STRING .OUT('CL(' * ');') .,

OUTPUT = ('.OUT' '(' $OUT1 ')' 
       / '<' $OUT1 '>'
       / '.LABEL' .OUT('LB();') OUT1) .OUT('OUT();') .,

EX3 = .ID .OUT('CLL(' * ',___' *1 ');') 
    / .STRING .OUT('TST(' * ');') 
    / '.ID' .OUT('ID();') 
    / '.NUMBER' .OUT('NUM();') 
    / '.STRING' .OUT('SR();') 
    / '(' EX1 ')' 
    / '.EMPTY' .OUT('SET') 
    / '$' .LABEL *1 .OUT(':;') EX3 .OUT('BT(' *1 ');') .OUT('SET();').,

EX2 = (EX3 .OUT('BF(' *1 ');') / OUTPUT)
    $(EX3 .OUT('BE();') / OUTPUT)
    .LABEL *1 .OUT(':;') .,

EX1 = EX2 $(('/' / '|') .OUT('BT(' *1 ');') EX2) .LABEL *1 .OUT(':;') .,

ST = .ID .LABEL * .OUT(':;') '=' EX1 ('.,' / ';') .OUT('R();') .,

PROGRAM = '.SYNTAX' .ID 
	.OUT('#include <meta2.h>')
	.OUT('void run() {')
	.OUT('ADR(' * ');') $ ST '.END' .OUT('}') .,

.END
